*TITLE1 "EXAMPLEJ01.SAS--Region x Gender power plot for N=20,40,80 with 5 age groups";

LIBNAME IN01 "..\DATA\";
FILENAME OUT01 ".\EXAMPLEJ01.CGM";

PROC IML SYMSIZE=2000 WORKSIZE=2000;
%INCLUDE "..\IML\POWERLIB203.IML"/NOSOURCE2;

OPT_ON = {GG HF BOX UN TOTAL_N ORTHU NOPRINT};
OPT_OFF={WARN ALPHA};

USE IN01.P0104;
READ ALL VAR {ANT LEFT POST RIGHT} WHERE(_TYPE_ = "COV") INTO INSIGMA;

ALPHA = .05/6;
SIGMA = ROUND(INSIGMA,.0001);

ESSENCEX = I(10);
REPN = {2 4 8};

P = 4;
Q = 2;
BETARG= J(2,4, 3.2) + {.30}#({ -1  0 1  0,
                               -1  0 1  0}) ;
BETASCAL={1};

C = {1 -1} @ J(1,5,1);

REGION = {1,2,3,4};
RUN UPOLY1(REGION,"REGION",U1,REGU);
U=U1;

DO DELTA=0 TO .20 BY .0025;
  BETARGD=BETARG + (J(2,2,0)||(DELTA//(-DELTA))||J(2,1,0));
  BETA= BETARGD @ J(5,1,1) ;
  RUN POWER;
  HOLDALL=HOLDALL//( _HOLDPOWER||J(NROW(_HOLDPOWER),1,DELTA) );
END;

*Creation of dataset from power calculations*;

NAME={"SIGSC" "BETASC" "N" "P_MULT" "EPS" "P_UN" "EXEPS_HF" "P_HF" "EXEPS_GG" "P_GG" "P_BOX" "DELTA"};
CREATE ONE FROM HOLDALL [COLNAME = NAME];
APPEND FROM HOLDALL;
CLOSE ONE;

PROC SORT DATA=ONE OUT=TWO;
BY N DELTA;

DATA THREE;
     MERGE TWO( WHERE=(N=20) RENAME=(P_GG=P20) )
           TWO( WHERE=(N=40) RENAME=(P_GG=P40) )
           TWO( WHERE=(N=80) RENAME=(P_GG=P80) );
           
*Power curves for N=20,40,80*;

GOPTIONS GSFNAME=OUT01 DEVICE=CGMOF97P
CBACK=WHITE COLORS=(BLACK) HORIGIN=0IN VORIGIN=0IN
HSIZE=5IN VSIZE=3IN HTEXT=12PT FTEXT=TRIPLEX;

SYMBOL1 I=SPLINE V=NONE L=1 W=2 R=3;

AXIS1 ORDER=(0 TO 1.0 BY .20)    W=3 MINOR=NONE MAJOR=(W=2)
      LABEL=(ANGLE=-90 ROTATE=90);
AXIS2 LABEL=("Delta") ORDER=(0 TO .20 BY .05) W=3 MINOR=NONE MAJOR=(W=2);

PROC GPLOT DATA=THREE;
PLOT (P20 P40 P80)*DELTA/ OVERLAY NOFRAME
       VZERO VAXIS=AXIS1 HZERO HAXIS=AXIS2 NOLEGEND;
LABEL P20 ="Power" P40 ="Power" P80 ="Power";

RUN; QUIT; RUN;